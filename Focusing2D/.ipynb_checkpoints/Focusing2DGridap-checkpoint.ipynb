{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": []
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Gridap, Gridap.Geometry, Gridap.Fields, GridapGmsh\n",
    "using LinearAlgebra\n",
    "using NLopt\n",
    "using CairoMakie, GridapMakie\n",
    "using ChainRulesCore, Zygote\n",
    "import ChainRulesCore: rrule\n",
    "NO_FIELDS = ZeroTangent()\n",
    "\n",
    "λ = 532      # Wavelength (nm)\n",
    "L = 600      # Width of the numerical cell (excluding PML) (nm)\n",
    "h1 = 600     # Height of the air region below the source (nm)\n",
    "h2 = 200     # Height of the air region above the source (nm)\n",
    "dpml = 300   # Thickness of the PML (nm)\n",
    "\n",
    "n_metal = 0.054 + 3.429im # Silver refractive index at λ = 532 nm\n",
    "n_air = 1    # Air refractive index\n",
    "μ = 1        # Magnetic permeability\n",
    "k = 2*π/λ    # Wavenumber (nm^-1)\n",
    "\n",
    "\n",
    "model = GmshDiscreteModel(\"../models/RecCirGeometry.msh\")\n",
    "\n",
    "order = 1\n",
    "reffe = ReferenceFE(lagrangian, Float64, order)\n",
    "V = TestFESpace(model, reffe, dirichlet_tags = [\"DirichletEdges\", \"DirichletNodes\"], vector_type = Vector{ComplexF64})\n",
    "U = V   # mathematically equivalent to TrialFESpace(V,0)\n",
    "\n",
    "degree = 2\n",
    "Ω = Triangulation(model)\n",
    "dΩ = Measure(Ω, degree)\n",
    "\n",
    "Γ_s = BoundaryTriangulation(model; tags = [\"Source\"]) # Source line \n",
    "dΓ_s = Measure(Γ_s, degree)\n",
    "\n",
    "Ω_d = Triangulation(model, tags=\"Design\")\n",
    "dΩ_d = Measure(Ω_d, degree)\n",
    "\n",
    "Ω_c = Triangulation(model, tags=\"Center\")\n",
    "dΩ_c = Measure(Ω_c, degree)\n",
    "\n",
    "p_reffe = ReferenceFE(lagrangian, Float64, 0)\n",
    "Q = TestFESpace(Ω_d, p_reffe, vector_type = Vector{Float64})\n",
    "P = Q\n",
    "np = num_free_dofs(P) # Number of cells in design region (number of design parameters)\n",
    "\n",
    "pf_reffe = ReferenceFE(lagrangian, Float64, 1)\n",
    "Qf = TestFESpace(Ω_d, pf_reffe, vector_type = Vector{Float64})\n",
    "Pf = Qf\n",
    "fem_params = (; V, U, Q, P, Qf, Pf, np, Ω, dΩ, dΩ_d, dΩ_c, dΓ_s)\n",
    "\n",
    "\n",
    "R = 1e-10\n",
    "LHp=(L/2, h1+h2)   # Start of PML for x,y > 0\n",
    "LHn=(L/2, 0)       # Start of PML for x,y < 0\n",
    "phys_params = (; k, n_metal, n_air, μ, R, dpml, LHp, LHn)\n",
    "\n",
    "# PML coordinate streching functions\n",
    "function s_PML(x; phys_params)\n",
    "    σ = -3 / 4 * log(phys_params.R) / phys_params.dpml / phys_params.n_air\n",
    "    xf = Tuple(x)\n",
    "    u = @. ifelse(xf > 0 , xf - phys_params.LHp, - xf - phys_params.LHn)\n",
    "    return @. ifelse(u > 0,  1 + (1im * σ / phys_params.k) * (u / phys_params.dpml)^2, $(1.0+0im))\n",
    "end\n",
    "\n",
    "function ds_PML(x; phys_params)\n",
    "    σ = -3 / 4 * log(phys_params.R) / phys_params.dpml / phys_params.n_air\n",
    "    xf = Tuple(x)\n",
    "    u = @. ifelse(xf > 0 , xf - phys_params.LHp, - xf - phys_params.LHn)\n",
    "    ds = @. ifelse(u > 0, (2im * σ / phys_params.k) * (1 / phys_params.dpml)^2 * u, $(0.0+0im))\n",
    "    return ds.*sign.(xf)\n",
    "end\n",
    "\n",
    "struct Λ{PT} <: Function\n",
    "    phys_params::PT\n",
    "end\n",
    "\n",
    "function (Λf::Λ)(x)\n",
    "    s_x,s_y = s_PML(x; Λf.phys_params)\n",
    "    return VectorValue(1/s_x, 1/s_y)\n",
    "end\n",
    "\n",
    "# Define the derivative for the Λ factor\n",
    "Fields.∇(Λf::Λ) = x -> TensorValue{2, 2, ComplexF64}(-(Λf(x)[1])^2 * ds_PML(x; Λf.phys_params)[1], 0, 0, -(Λf(x)[2])^2 * ds_PML(x; Λf.phys_params)[2])\n",
    "\n",
    "\n",
    "r = 5/sqrt(3)               # Filter radius\n",
    "β = 32.0                    # β∈[1,∞], threshold sharpness\n",
    "η = 0.5                     # η∈[0,1], threshold center    \n",
    "\n",
    "a_f(r, u, v) = r^2 * (∇(v) ⋅ ∇(u))\n",
    "\n",
    "function Filter(p0; r, fem_params)\n",
    "    ph = FEFunction(fem_params.P, p0)\n",
    "    op = AffineFEOperator(fem_params.Pf, fem_params.Qf) do u, v\n",
    "        ∫(a_f(r, u, v))fem_params.dΩ_d + ∫(v * u)fem_params.dΩ_d, ∫(v * ph)fem_params.dΩ_d\n",
    "      end\n",
    "    pfh = solve(op)\n",
    "    return get_free_dof_values(pfh)\n",
    "end\n",
    "\n",
    "function Threshold(pfh; β, η)\n",
    "    return ((tanh(β * η) + tanh(β * (pfh - η))) / (tanh(β * η) + tanh(β * (1.0 - η))))         \n",
    "end\n",
    "\n",
    "\n",
    "ξd(p, n_air, n_metal)= 1 / (n_air + (n_metal - n_air) * p)^2 - 1 / n_air^2 # in the design region\n",
    "\n",
    "a_base(u, v; phys_params) = (1 / phys_params.n_air^2) * ((∇ .* (Λ(phys_params) * v)) ⊙ (Λ(phys_params) .* ∇(u))) - (phys_params.k^2 * phys_params.μ * (v * u))\n",
    "\n",
    "a_design(u, v, pth; phys_params) = ((p -> ξd(p, phys_params.n_air, phys_params.n_metal)) ∘ pth) * (∇(v) ⊙ ∇(u))\n",
    "\n",
    "function MatrixA(pth; phys_params, fem_params)\n",
    "    A_mat = assemble_matrix(fem_params.U, fem_params.V) do u, v\n",
    "        ∫(a_base(u, v; phys_params))fem_params.dΩ + ∫(a_design(u, v, pth; phys_params))fem_params.dΩ_d\n",
    "    end\n",
    "    return lu(A_mat)\n",
    "end\n",
    "\n",
    "\n",
    "function MatrixOf(fem_params)\n",
    "    x0 = VectorValue(0,300)  # Position of the field to be optimized\n",
    "    δ = 1                    \n",
    "    return assemble_matrix(fem_params.U, fem_params.V) do u, v\n",
    "        ∫((x->(1/(2*π)*exp(-norm(x - x0)^2 / 2 / δ^2))) * (∇(u) ⋅ ∇(v)) )fem_params.dΩ_c\n",
    "    end\n",
    "end\n",
    "\n",
    "\n",
    "Dptdpf(pf, β, η) = β * (1.0 - tanh(β * (pf - η))^2) / (tanh(β * η) + tanh(β * (1.0 - η)))\n",
    "\n",
    "Dξdpf(pf, n_air, n_metal, β, η)= 2 * (n_air - n_metal) / (n_air + (n_metal - n_air) * Threshold(pf; β, η))^3 * Dptdpf(pf, β, η)\n",
    "\n",
    "DAdpf(u, v, pfh; phys_params, β, η) = ((p -> Dξdpf(p, phys_params.n_air, phys_params.n_metal, β, η)) ∘ pfh) * (∇(v) ⊙ ∇(u))\n",
    "\n",
    "function gf_pf(pf_vec; β, η, phys_params, fem_params)\n",
    "    pfh = FEFunction(fem_params.Pf, pf_vec)\n",
    "    pth = (pf -> Threshold(pf; β, η)) ∘ pfh\n",
    "    A_mat = MatrixA(pth; phys_params, fem_params)\n",
    "    b_vec = assemble_vector(v->(∫(v)fem_params.dΓ_s), fem_params.V)\n",
    "    u_vec = A_mat \\ b_vec\n",
    "    \n",
    "    O_mat = MatrixOf(fem_params)\n",
    "    real(u_vec' * O_mat * u_vec)\n",
    "end\n",
    "\n",
    "function rrule(::typeof(gf_pf), pf_vec; β, η, phys_params, fem_params)\n",
    "    function U_pullback(dgdg)\n",
    "      NO_FIELDS, dgdg * Dgfdpf(pf_vec; β, η, phys_params, fem_params)\n",
    "    end\n",
    "    gf_pf(pf_vec; β, η, phys_params, fem_params), U_pullback\n",
    "end\n",
    "\n",
    "function Dgfdpf(pf_vec; β, η, phys_params, fem_params)\n",
    "    pfh = FEFunction(fem_params.Pf, pf_vec)\n",
    "    pth = (pf -> Threshold(pf; β, η)) ∘ pfh\n",
    "    A_mat = MatrixA(pth; phys_params, fem_params)\n",
    "    b_vec = assemble_vector(v->(∫(v)fem_params.dΓ_s), fem_params.V)\n",
    "    u_vec = A_mat \\ b_vec\n",
    "    O_mat = MatrixOf(fem_params)\n",
    "    \n",
    "    uh = FEFunction(fem_params.U, u_vec)\n",
    "    w_vec =  A_mat' \\ (O_mat * u_vec)\n",
    "    wconjh = FEFunction(fem_params.U, conj(w_vec)) \n",
    "    \n",
    "    l_temp(dp) = ∫(real(-2 * DAdpf(uh, wconjh, pfh; phys_params, β, η)) * dp)fem_params.dΩ_d\n",
    "    dgfdpf = assemble_vector(l_temp, fem_params.Pf)\n",
    "    return dgfdpf\n",
    "end\n",
    "\n",
    "function pf_p0(p0; r, fem_params)\n",
    "    pf_vec = Filter(p0; r, fem_params)\n",
    "    pf_vec[pf_vec .< 0] .= 0\n",
    "    pf_vec[pf_vec .> 1] .= 1.0\n",
    "    pf_vec\n",
    "end\n",
    "\n",
    "function rrule(::typeof(pf_p0), p0; r, fem_params)\n",
    "  function pf_pullback(dgdpf)\n",
    "    NO_FIELDS, Dgdp(dgdpf; r, fem_params)\n",
    "  end\n",
    "  pf_p0(p0; r, fem_params), pf_pullback\n",
    "end\n",
    "\n",
    "function Dgdp(dgdpf; r, fem_params)\n",
    "    Af = assemble_matrix(fem_params.Pf, fem_params.Qf) do u, v\n",
    "        ∫(a_f(r, u, v))fem_params.dΩ_d + ∫(v * u)fem_params.dΩ_d\n",
    "    end\n",
    "    wvec = Af' \\ dgdpf\n",
    "    wh = FEFunction(fem_params.Pf, wvec)\n",
    "    l_temp(dp) = ∫(wh * dp)fem_params.dΩ_d\n",
    "    return assemble_vector(l_temp, fem_params.P)\n",
    "end\n",
    "\n",
    "function gf_p(p0::Vector; r, β, η, phys_params, fem_params)\n",
    "    pf_vec = pf_p0(p0; r, fem_params)\n",
    "    gf_pf(pf_vec; β, η, phys_params, fem_params)\n",
    "end\n",
    "\n",
    "function gf_p(p0::Vector, grad::Vector; r, β, η, phys_params, fem_params)\n",
    "    if length(grad) > 0\n",
    "        dgdp, = Zygote.gradient(p -> gf_p(p; r, β, η, phys_params, fem_params), p0)\n",
    "        grad[:] = dgdp\n",
    "    end\n",
    "    gvalue = gf_p(p0::Vector; r, β, η, phys_params, fem_params)\n",
    "    open(\"gvalue.txt\", \"a\") do io\n",
    "        write(io, \"$gvalue \\n\")\n",
    "    end\n",
    "    gvalue\n",
    "end\n",
    "\n",
    "\n",
    "function gf_p_optimize(p_init; r, β, η, TOL = 1e-4, MAX_ITER = 500, phys_params, fem_params)\n",
    "    ##################### Optimize #################\n",
    "    opt = Opt(:LD_MMA, fem_params.np)\n",
    "    opt.lower_bounds = 0\n",
    "    opt.upper_bounds = 1\n",
    "    opt.ftol_rel = TOL\n",
    "    opt.maxeval = MAX_ITER\n",
    "    opt.max_objective = (p0, grad) -> gf_p(p0, grad; r, β, η, phys_params, fem_params)\n",
    "\n",
    "    (g_opt, p_opt, ret) = optimize(opt, p_init)\n",
    "    @show numevals = opt.numevals # the number of function evaluations\n",
    "    return g_opt, p_opt\n",
    "end\n",
    "\n",
    "p_opt = fill(0.4, fem_params.np)   # Initial guess\n",
    "β_list = [8.0, 16.0, 32.0]\n",
    "\n",
    "g_opt = 0\n",
    "TOL = 1e-8\n",
    "MAX_ITER = 100\n",
    "for bi = 1 : 3\n",
    "    β = β_list[bi]\n",
    "    g_opt, p_temp_opt = gf_p_optimize(p_opt; r, β, η, TOL, MAX_ITER, phys_params, fem_params)\n",
    "    global p_opt = p_temp_opt\n",
    "end\n",
    "@show g_opt\n",
    "\n",
    "p0 = p_opt\n",
    "\n",
    "pf_vec = pf_p0(p0; r, fem_params)\n",
    "pfh = FEFunction(fem_params.Pf, pf_vec)\n",
    "pth = (pf -> Threshold(pf; β, η)) ∘ pfh\n",
    "A_mat = MatrixA(pth; phys_params, fem_params)\n",
    "b_vec = assemble_vector(v->(∫(v)fem_params.dΓ_s), fem_params.V)\n",
    "u_vec = A_mat \\ b_vec\n",
    "uh = FEFunction(fem_params.U, u_vec)\n",
    "\n",
    "fig, ax, plt = plot(fem_params.Ω, pth, colormap = :binary)\n",
    "Colorbar(fig[1,2], plt)\n",
    "ax.aspect = AxisAspect(1)\n",
    "ax.title = \"Design Shape\"\n",
    "rplot = 110 # Region for plot\n",
    "limits!(ax, -rplot, rplot, (h1)/2-rplot, (h1)/2+rplot)\n",
    "save(\"shape.png\", fig)\n",
    "\n",
    "maxe = 30 # Maximum electric field magnitude compared to the incident plane wave\n",
    "e1=abs2(phys_params.n_air^2)   \n",
    "e2=abs2(phys_params.n_metal^2)\n",
    "\n",
    "fig, ax, plt = plot(fem_params.Ω, 2*(sqrt∘(abs((conj(∇(uh)) ⋅ ∇(uh))/(CellField(e1,fem_params.Ω) + (e2 - e1) * pth)))), colormap = :hot, colorrange=(0, maxe))\n",
    "Colorbar(fig[1,2], plt)\n",
    "ax.title = \"|E|\"\n",
    "ax.aspect = AxisAspect(1)\n",
    "limits!(ax, -rplot, rplot, (h1)/2-rplot, (h1)/2+rplot)\n",
    "save(\"Field.png\", fig)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.6.0",
   "language": "julia",
   "name": "julia-1.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
